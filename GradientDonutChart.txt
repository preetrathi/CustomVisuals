DepositGradientDonut = 
VAR DonutFill = DIVIDE([TotalDeposits], [TotalBalance], 0) * 100
VAR DonutText = FORMAT(DonutFill, "0.0") & "%"
VAR Circumference = 2 * PI() * 80
VAR Fill_Length = (Circumference * DonutFill) / 100
VAR Gap_Length = Circumference - Fill_Length
VAR SVG_DATA_URL = "data:image/svg+xml;utf8,"
VAR SVG_Start = "<svg xmlns='http://www.w3.org/2000/svg' viewBox='0 0 250 250'>"
VAR SVG_Data =
    "<defs>
        <linearGradient id='gradient' x1='0%' y1='0%' x2='100%' y2='0%'>
            <stop offset='0%' style='stop-color: #f04393' />
            <stop offset='100%' style='stop-color: #f9c449' />
        </linearGradient>
    </defs>

    <circle cx='125' cy='125' r='80' fill='none' stroke='#f7f3f3' stroke-width='15' />
    <circle cx='125' cy='125' r='80' fill='none' stroke='url(#gradient)' stroke-width='15'
        stroke-dasharray='" & FORMAT(Fill_Length, "0.##") & "," & FORMAT(Gap_Length, "0.##") & "'
        transform='rotate(-90 125 125)' stroke-linecap='round' />
    
    <text x='125' y='135' text-anchor='middle' font-size='24' fill='#333'>" & DonutText & "</text>"

VAR SVG_End = "</svg>"

RETURN SVG_DATA_URL & SVG_Start & SVG_Data & SVG_End
